grammar de.jugh.ArkanoidDsl with org.eclipse.xtext.xbase.Xbase

generate arkanoidDsl "http://www.jugh.de/ArkanoidDsl"

import "http://www.eclipse.org/xtext/xbase/Xbase" as xbase

GameContentProvider :
	((game = ContentDefinition) | (level += Level)* | (bricks += Brick)+) | rowCreators += RowCreator+
;

ContentDefinition :
	{ContentDefinition}
	'Content'
	levels += [Level]*
;

Level:
	'Level'
	name = ID
	rows += Row*
;

RowCreator:
	'RowCreator' name=ID creator=XBlockExpression
;
 

Row :
	'Row' 
		(bricks += BrickInRow+ |
		'creator' rowCreatorRef = [RowCreator])
;


BrickInRow:
	instance = Brick |
	{BrickReference} reference = [Brick]
;

Brick :
	'B' (name=ID)? EXPR_START color=BRICK_COLOR EXPR_END
;

enum BRICK_COLOR :
	BLUE | YELLOW | RED | GREEN
;

terminal EXPR_START : '{';
terminal EXPR_END : '}';